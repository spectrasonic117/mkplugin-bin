plugins {
    id("io.papermc.paperweight.userdev") version "${gradlePaperweight}"
    id "com.gradleup.shadow" version "${gradleShadow}"
    id "java"
}

repositories {
    gradlePluginPortal() 
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"

    }
    maven { 
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    // compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT") // Paper
    paperweight.paperDevBundle("${minecraftVersion}-R0.1-SNAPSHOT")

    // CommandAPI
    // compileOnly "dev.jorel:commandapi-bukkit-core:10.1.2" // for External Dependencies
    implementation "dev.jorel:commandapi-bukkit-shade-mojang-mapped:${CommandAPI}"

    // Lombok
    compileOnly "org.projectlombok:lombok:${Lombok}"
    annotationProcessor "org.projectlombok:lombok:${Lombok}"

    // Minimessage - Adventure
    implementation "net.kyori:adventure-text-minimessage:${Minimessage}"
    implementation "net.kyori:adventure-api:${Minimessage}"

}

shadowJar {
    relocate("dev.jorel.commandapi", "${group}.commandapi")
    destinationDirectory = file("${rootDir}/out")
    archiveFileName = "${rootProject.name}-${version}.jar"
}

build.dependsOn shadowJar
build.finalizedBy reobfJar

def targetJavaVersion = "${javaVersion}"
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}